import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'com.android.application'

android {
    compileSdkVersion 31
    buildToolsVersion "31.0.0"

    defaultConfig {
        applicationId "net.zakume.game"
        minSdkVersion 21
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 31
        versionCode 2
        versionName "2.1"

        externalNativeBuild {
            if (PROP_BUILD_TYPE == 'ndk-build') {
                ndkBuild {
                    targets 'cocos2dcpp'
                    // arguments 'NDK_TOOLCHAIN_VERSION=clang'
                    arguments '-j' + Runtime.runtime.availableProcessors()
                    arguments 'NDK_TOOLCHAIN_VERSION=4.9'
                    // arguments 'APP_PLATFORM=android-'+PROP_APP_PLATFORM

                    def module_paths = [
                            project.file("../../../..").absolutePath,
                            project.file("../../../../cocos2dx").absolutePath,
                            project.file("../../../../cocos2dx/platform/third_party/android/prebuilt").absolutePath,
                            project.file("../../../../external").absolutePath,
                            project.file("../../../../extensions").absolutePath,
                            project.file("../../../../CocosDenshion/android").absolutePath
                    ]
                    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                        // should use '/'
                        module_paths = module_paths.collect { it.replaceAll('\\\\', '/') }
                        arguments 'NDK_MODULE_PATH=' + module_paths.join(";")
                    } else {
                        arguments 'NDK_MODULE_PATH=' + module_paths.join(':')
                    }
                }
            }
        }

        ndk {
            abiFilters = []
            abiFilters.addAll(PROP_APP_ABI.split(':').collect { it as String })
        }
    }

    sourceSets.main {
        java.srcDir "src"
        res.srcDir "res"
        jniLibs.srcDir "libs"
        manifest.srcFile "AndroidManifest.xml"
        assets.srcDir "assets"
    }

    externalNativeBuild {
        if (PROP_BUILD_TYPE == 'ndk-build') {
            ndkBuild {
                path "./jni/Android.mk"
            }
        }
    }

    signingConfigs {
        debug {
            if (project.hasProperty("DEBUG_STORE_FILE")) {
                storeFile file(DEBUG_STORE_FILE)
                storePassword DEBUG_STORE_PASSWORD
                keyAlias DEBUG_KEY_ALIAS
                keyPassword DEBUG_KEY_PASSWORD
            }
        }
        release {
            if (project.hasProperty("RELEASE_STORE_FILE")) {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false // NOTE: Crash when minifyEnabled is true
            jniDebuggable true
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (project.hasProperty("RELEASE_STORE_FILE")) {
                signingConfig signingConfigs.release
            }
        }
        debug {
            minifyEnabled false
            jniDebuggable true
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':libcocos2dx')
}

task cleanAssets(type: Delete) {
    delete 'assets'
}

task copLuaFiles(type: Copy) {
    from '../../lua'
    into 'assets/lua'
}

task copyAssets(type: Copy, dependsOn: copLuaFiles) {
    from '../../Resources'
    into 'assets'
}

clean.dependsOn cleanAssets
preBuild.dependsOn copyAssets
